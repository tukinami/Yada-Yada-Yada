// ******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
// ******************************************************************************

// ******************************************************************************
// こちらから話しかける
// ******************************************************************************

// この関数は現状メニューから呼ばれます
StartCommunicate
{
    // 以下の関数は、隣に立っているゴーストを探して、
    // TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
    // yaya_tmpl_utilにあります。
    AYATEMPLATE.ExecuteTalkToTalk
}

// その他の人に話しかける
TalkToOther
{
    _escaped_name = SHIORI3FW.EscapeAllTags(res_reference0)
    "\u\s[10]\h\s[0]こ、\w5こんにちは、\w5%(_escaped_name)さん。\e"
}

TalkToNobody
{
    '\u\s[10]\h\s[6]…\w5…\w5誰もいないよ。\e'
}

// ******************************************************************************
// 話しかけられたので返事する
// ******************************************************************************

// ------------------------------------------------------------------------------
// OnCommunicateイベント
// ------------------------------------------------------------------------------
OnCommunicate
{
    if reference[0] == "user" || reference[0] == "User" {
        // ---- ユーザーからの話しかけ
        ReplyToUser
    }
    else {
        // ---- ゴーストからの話しかけ
        // 以下の関数は、ReplyTo_??? 関数を呼び出すものです。
        // yaya_tmpl_utilにあります。
        AYATEMPLATE.ExecuteReplyToTalk(reference[0])
    }
}

// ------------------------------------------------------------------------------
// ユーザーからの話しかけ
// ------------------------------------------------------------------------------
ReplyToUser
{
    '\u\s[10]\h\s[0]？ \w9なんでしょう？\w9\w9'
}

// ------------------------------------------------------------------------------
// ゴーストからの話しかけ
// ------------------------------------------------------------------------------

// その他の人から話しかけられた
ReplyToOther
{
    SHIORI3FW.RegisterTempVar('communicate_target_ghost')
    _greeting = ''
    _greeting_ex = ''

    if RE_MATCH(reference[1], '.*(おはよう|オハヨウ|お早う).*') > 0 {
        _greeting = 'おはようございます'
        if '朝' !_in_ Time.hour.to_slot {
            _greeting_ex = '\w9\w9\n\s[1]…\w5…\w5朝？'
        }
    }
    elseif RE_MATCH(reference[1], '.*(こんにち(は|わ)|(今日(は|わ|和))|(コンニチ(ワ|ハ))).*') > 0 {
        _greeting = 'こんにちは'
    }
    elseif RE_MATCH(reference[1], '.*(こんちは|コンチワ).*') > 0 {
        _greeting = 'こんにちは'
    }
    elseif RE_MATCH(reference[1], '.*(こんばん(は|わ)|(今晩(は|わ|和))|(コンバン(ワ|ハ))).*') > 0 {
        _greeting = 'こんばんは'
        if '夜' !_in_ Time.hour.to_slot {
            _greeting_ex = '\w9\w9\n\s[1]…\w5…\w5夜？'
        }
    }
    elseif RE_MATCH(reference[1], '.*(ハロー|Hello|hello|HELLO|Ｈｅｌｌｏ|ｈｅｌｌｏ|ＨＥＬＬＯ).*') > 0 {
        _greeting = 'こんにちは'
    }
    elseif RE_MATCH(reference[1], '.*(どうも).*') > 0 {
        // とりあえず汎用
        _greeting = 'こんにちは'
    }

    '\u\s[10]'
    --
    if STRLEN(_greeting) != 0 {
        res_reference[0] = reference[0]

        if communicate_target_ghost == reference[0] {
            communicate_target_ghost = ''
            '\h\s[1]ご丁寧に、\w5ありがとうございます。'
        }
        else {
            communicate_target_ghost = reference[0]
            "\h\s[0]%(_greeting)、\w5%(SHIORI3FW.EscapeAllTags(reference[0]))さん。%(_greeting_ex)"
        }
    }
    else {
        '\h\s[6]？\w9今何て？'
    }
    --
    '\w9\w9\e'
}
