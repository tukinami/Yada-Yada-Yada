// ******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
// ******************************************************************************

// ******************************************************************************
// マウス処理部
//
// 以下、実際のトークを記述します。関数名は以下の規則になっています。
// [種別] + [スコープ] + [部位]
// 種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
// スコープは、\0,\1,といったスコープ番号です。
// 部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
// 部位はありません。
// ******************************************************************************

// ******************************************************************************
// 撫で反応
// ******************************************************************************
MouseMove0Head : nonoverlap_pool
{
    if flag_onclose > 0 {
        MouseMove0Head.onclose()
        return
    }

    res_marker = '頭を撫でる'

    '\t\*'
    --
    '\u\s[10]\h\s[15]えへへ…\w5…\w5。'
    '\u\s[10]\h\s[13]ん…\w5…\w5。'
    '\u\s[10]\h\s[14]…\w5…\w5。'
    '\u\s[10]\h\s[14]…\w5…\w5もっとしてほしい、\w5です。'
    '\u\s[10]\h\s[15]えへ、\w5落ち着きます。'
    --
    '\w9\w9\e'
}

MouseMove0Head.onclose
{
    res_marker = '**頭を撫でる**'

    count_onclose_special++

    '\t\*'
    --
    if Status.is_resolved_onclose_term() {
        '\u\s[10]\h\s[12]！！\w9\s[15]\n…\w5…\w5えへへ、\w5ありがとうございます。\w9\w9\s[8]\n分かりました。\w9いってらっしゃいませ。\w9\w9\u(なんだか空気が柔らかくなったな)\w9\nうん、\w5いってきます。'
    }
    else {
        '\u\s[10]\h\s[12]！！\w9\s[1]あ、\w5ありがとうございます。\w9\w9\s[4]\n…\w5…\w5\w9\s[0]分かりました。\w9いってらっしゃいませ。\w9\w9\u(気が済んだのかな？)\w9\nうん、\w5いってきます。'
    }
    --
    '\w9\w9\w9\-\e'
}

// ******************************************************************************
// つつかれ反応
// ******************************************************************************

// 本体側のつつかれ反応
MouseDoubleClick0
{
    // 顔以外のつつきでメニュー展開
    OpenMenu
}

// ******************************************************************************
// マウス処理部コア
// AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
// 1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
// 変更の必要はありません。
// ******************************************************************************

OnMouseMove
{
    AYATEMPLATE.MouseMoveWheelFunc(0, 60, 'MouseMove')
}

OnMouseWheel
{
    _result = ''
    if TOINT(reference[2]) > 0 {
        _result = AYATEMPLATE.MouseMoveWheelFunc(1, 4, 'MouseWheelUp')
    }
    else {
        _result = AYATEMPLATE.MouseMoveWheelFunc(1, 4, 'MouseWheelDown')
    }

    if _result == '' {
        _result = AYATEMPLATE.MouseMoveWheelFunc(1, 4, 'MouseWheel')
    }

    _result
}

OnMouseDoubleClick
{
    AYATEMPLATE.MouseEventExec('MouseDoubleClick')
}
